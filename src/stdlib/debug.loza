#
# debug.loza
#
# The Loza Project
# Copyright 2020-2024 Wsoum <wosoum@gmail.com>
# Copyright 2020-2021 parsa shahmaleki <parsampsh@gmail.com>
#
# This file is part of Loza.
#
# Loza is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# Loza is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with Loza.  If not, see <https://www.gnu.org/licenses/>.
#########################################################################

@doc """\
Starts the debug shell. \
Gets a message as string"""
func debug()
	$tmp_loza_debug_message = str(^)

	# check is debug enabled
	if not is_defined('DEBUG')
		return
	endif
	if not DEBUG
		return
	endif

	# print the debug message
	if is_defined('DEBUG_HEADER_TITLE')
		println DEBUG_HEADER_TITLE
	else
		println 'Debug started (' + $tmp_loza_debug_message + ') at ' + python('self.mem = self.frames[-3]["commands"][self.frames[-3]["current_step"]]["file_path"]') + ':' + str(python('self.mem = self.frames[-3]["commands"][self.frames[-3]["current_step"]]["line_number"]'))
	endif

	# start getting commands from user
	while true
		$tmp_loza_debug_cmd = read('> ')->strip()

		if $tmp_loza_debug_cmd == 'n'
			break
		endif

		try tmp_loza_debug_error
			eval($tmp_loza_debug_cmd)
		endtry
		goto after_tmp_loza_debug_error; section tmp_loza_debug_error
			$tmp_loza_debug_ex = ^
			println $tmp_loza_debug_ex->type + ': ' + $tmp_loza_debug_ex->message
		section after_tmp_loza_debug_error
	endwhile

	if not is_defined('DEBUG_HEADER_TITLE')
		println 'Debug finished.'
	endif
endfunc
